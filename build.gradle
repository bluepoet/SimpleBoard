apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'tomcat'
apply plugin: 'eclipse-wtp'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
	javaVersion = '1.7'
	springVersion = '4.1.6.RELEASE'
}

buildDir = 'target'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
	}
	test {
		java {
			srcDirs = ['test/java']
		}
	}
}

task initProject << {
	sourceSets*.java.srcDirs*.each { it.mkdirs() }
	sourceSets*.resources.srcDirs*.each { it.mkdirs() }

	def webAppDir = "$rootDir/src/main/webapp"

	file(webAppDir + "/WEB-INF/view").mkdirs()
}

eclipse {
	classpath {
		downloadSources = true
		defaultOutputDir = file("${buildDir}/classes")
		file {
			withXml {
				xml -> def node = xml.asNode() 
					node.remove(node.find { it.@path == 'org.eclipse.jst.j2ee.internal.web.container'})
					node.appendNode('classpathentry', [kind:'con',path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
			}
		}  		
	}

	jdt {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
	}

	wtp {
		component {
			contextPath ='/'
		}
		facet {
			facet name: 'jst.web', version: '3.1'
			facet name: 'jst.java', version: '1.7'
		}
	}
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:1.0'
    }
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
	compile 'org.springframework:spring-webmvc:4.1.6.RELEASE'
	compile 'org.springframework:spring-orm:4.1.6.RELEASE'
	
	compile 'org.springframework.data:spring-data-jpa:1.8.0.RELEASE'
	compile 'org.hibernate:hibernate-entitymanager:4.3.10.Final'
	runtime 'com.h2database:h2:1.4.187'

	compile 'org.apache.tomcat:tomcat-jdbc:8.0.24'

	runtime 'javax.servlet:jstl:1.2'
	providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    providedCompile 'javax.servlet.jsp:javax.servlet.jsp-api:2.3.1'

    compile 'org.slf4j:slf4j-api:1.7.6'
    compile 'ch.qos.logback:logback-classic:1.1.1'
	
	testCompile group: 'junit', name: 'junit', version:'4.12'
	testCompile group: 'org.springframework', name: 'spring-test', version:"4.1.6.RELEASE"

	String tomcatVersion = '8.0.24'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    tomcat "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
    providedCompile "org.apache.tomcat:tomcat-servlet-api:${tomcatVersion}"
}